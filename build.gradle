plugins {
  id 'java'
  id 'java-library'
  id 'application'
  id 'jacoco'
  id 'com.github.johnrengelman.shadow' version '5.2.0'
  id 'org.sonarqube' version '2.8'
}

mainClassName = 'com.issue.progress.TransformControl'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

ext {
  junitVersion = '5.6.2'
  sonarProjectName = 'ImplProgress'
  sonarProjectKey = 'com.issue.implprogress'
  jacocoDestinationFile = file("$buildDir/jacoco/test.exec")
}

repositories {
  mavenCentral()
}

test {
  useJUnitPlatform()
  jacoco {
    destinationFile = file(jacocoDestinationFile)
  }
  afterSuite { desc, result ->
    if (!desc.parent) {
      println "\nTest result: ${result.resultType}"
      println "Test summary: ${result.testCount} tests, " +
              "${result.successfulTestCount} succeeded, " +
              "${result.failedTestCount} failed, " +
              "${result.skippedTestCount} skipped"
    }
  }
}

dependencies {
  implementation "com.fasterxml.jackson.core:jackson-databind:2.11.0"
  implementation "org.apache.poi:poi-ooxml:4.1.2"
  implementation "org.apache.logging.log4j:log4j-core:2.13.3"
  implementation "org.apache.logging.log4j:log4j-api:2.13.3"
  implementation "info.picocli:picocli:4.3.2"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
  testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
  testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
  testImplementation "org.junit.platform:junit-platform-launcher:1.6.2"
  testImplementation "org.assertj:assertj-core:3.16.1"
  testCompileOnly "org.apiguardian:apiguardian-api:1.1.0"
}

def versionMajor = 1
def versionMinor = 4
def versionPatch = 0
def versionBuild = 5
version = "R${versionMajor}.${versionMinor}.${versionPatch}"

jar{
  manifest{
    attributes 'Specification-Title': rootProject.name,
    'Specification-Version': archiveVersion,
    'Implementation-Title': 'com.issue',
    'Implementation-Version': versionBuild,
    'Main-Class': 'com.issue.progress.TransformControl'
  }
}

shadowJar{
  setArchiveFileName('ImplProgress.jar')
}

jacoco {
  toolVersion = '0.8.2'
}

jacocoTestReport {
  reports {
    xml.enabled true
  }
}

sonarqube {
  properties {
    property "sonar.projectKey", "BranislavBeno_ImplProgress"
  }
}
